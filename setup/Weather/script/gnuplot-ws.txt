#
# Gnuplot input file for weather station data.
#
# Data are read from 3 files
#   Daily   data: ws_day.txt      , produced using:  $ wstation <database> -xd=1  > ws_day.txt 
#   Weekly  data: ws_week.txt     , produced using:  $ wstation <database> -xd=7  > ws_week.txt 
#   Monthly data: ws_month.txt    , produced using:  $ wstation <database> -xd=31 > ws_month.txt 
#
cd "../www"
set xdata time
set timefmt "%s"
set format x "%b-%d"
set grid linewidth 0.3
unset key
set size ratio 0.3
set terminal png small size 800, 330
#
# -----------DAY
set format x "%H:%M"
#
set yrange [*:*]
set title "Indoor Temperature [C]"
set output "itemp_day.png"
plot "ws_day.txt" using 1:2  with lines linewidth 1.5 
#
set title "Indoor Humidity [%]"
set output "ihumi_day.png"
set yrange [0:100] 
plot "ws_day.txt" using 1:3 with lines linewidth 1.5
#
set yrange [*:*]
set title "Outdoor Temperature [C]"
set output "otemp_day.png"
plot "ws_day.txt" using 1:4 with lines linewidth 1.5
#
set yrange [0:100] 
set title "Outdoor Humidity [%]"
set output "ohumi_day.png"
plot "ws_day.txt" using 1:5 with lines linewidth 1.5
#
set yrange [*:*]
set title "Pressure (hPa) - (sea level)"
set output "opres_day.png"
plot "ws_day.txt" using 1:6 with lines linewidth 1.5
#
set yrange [0:*]
set title "Rain intensity [mm/h] "
set output "irain_day.png"
plot "ws_day.txt" using 1:11 with lines linewidth 1.5
#
set key top
set yrange [0:*]
set title "Wind speed [m/s]"
set output "owspd_day.png"
plot "ws_day.txt" using 1:8 with lines linewidth 1 title "Gust", \
     "ws_day.txt" using 1:7 with lines linewidth 1.5 title "Average"
unset key
#
# ------------WEEK
#
set format x "%b-%d"
#
set key top
set yrange [*:*]
set title "Indoor Temperature [C]"
set output "itemp_week.png"
plot  "ws_week.txt" using 13:14 with lines linewidth 1.8 linecolor rgb "#008000" title "24h average", \
       "ws_week.txt" using 1:2  with lines linewidth 1.5 linecolor rgb "red"  title ""
unset key
#
set title "Indoor Humidity [%]"
set output "ihumi_week.png"
set yrange [0:100] 
plot "ws_week.txt" using 1:3 with lines linewidth 1.5
#
set key top
set yrange [*:*]
set title "Outdoor Temperature [C]"
set output "otemp_week.png"
plot "ws_week.txt" using 13:15 with lines linewidth 1.8  linecolor rgb "#008000"  title "24h average", \
     "ws_week.txt" using 1:4 with lines linewidth 1.5 linecolor rgb "red" title ""
unset key

#
set yrange [0:100] 
set title "Outdoor Humidity [%]"
set output "ohumi_week.png"
plot "ws_week.txt" using 1:5 with lines linewidth 1.5
#
set yrange [*:*]
set title "Pressure (hPa)  - (sea level)"
set output "opres_week.png"
plot "ws_week.txt" using 1:6 with lines linewidth 1.5
#
set key top
set yrange [0:*]
set title "Rain intensity [mm/h] "
set output "irain_week.png"
plot "ws_week.txt" using 1:12 with lines linewidth 1.5 linecolor rgb "#008000"  title "24h", \
     "ws_week.txt" using 1:11 with lines linewidth 1.5 linecolor rgb "red" title ""
unset key
#
set key top
set yrange [0:15]
set title "Wind speed [m/s]"
set output "owspd_week.png"
plot "ws_week.txt" using 1:8 with lines linewidth 1 title "Gust", \
     "ws_week.txt" using 1:7 with lines linewidth 1.5 title "Average"
unset key
#
# ------------MONTH
#
set key top
set yrange [*:*]
set title "Indoor Temperature [C]"
set output "itemp_month.png"
plot  "ws_month.txt" using 13:14 with lines linewidth 1.8  linecolor rgb "#008000"  title "24h average" , \
      "ws_month.txt" using 1:2  with lines linewidth 1.5 linecolor rgb "red"  title ""
unset key
#
#
set title "Indoor Humidity [%]"
set output "ihumi_month.png"
set yrange [0:100] 
plot "ws_month.txt" using 1:3 with lines linewidth 1.5
#
set key top
set yrange [*:*]
set title "Outdoor Temperature [C]"
set output "otemp_month.png"
plot "ws_month.txt" using 13:15 with lines linewidth 1.8  linecolor rgb "#008000"  title "24h average", \
     "ws_month.txt" using 1:4 with lines linewidth 1.5 linecolor rgb "red" title ""
unset key
#
set yrange [0:100] 
set title "Outdoor Humidity [%]"
set output "ohumi_month.png"
plot "ws_month.txt" using 1:5 with lines linewidth 1.5
#
set yrange [*:*]
set title "Pressure (hPa) - (sea level)"
set output "opres_month.png"
plot "ws_month.txt" using 1:6 with lines linewidth 1.5
#
set key top
set yrange [0:*]
set title "Rain intensity [mm/h] "
set output "irain_month.png"
plot "ws_month.txt" using 1:12 with lines linewidth 1.5 linecolor rgb "#008000"  title "24h", \
     "ws_month.txt" using 1:11 with lines linewidth 1.5 linecolor rgb "red" title ""
unset key
#
set key top
set yrange [0:15]
set title "Wind speed [m/s]"
set output "owspd_month.png"
plot "ws_month.txt" using 1:8 with lines linewidth 1 title "Gust", \
     "ws_month.txt" using 1:7 with lines linewidth 1.5 title "Average"
unset key
# 
# Produce polar plots of wind & direction
# We add 90 degrees to the direction angles, so that North is up on the plots
#
set terminal png small size 400, 400
set key top
set xdata
set format x
set xrange [-15:15]
set yrange [-15:15]
set size square
set polar
set grid polar
set angles degrees
set title "Wind [m/s] and direction (North up)"
#
set output "owdir_day.png"
plot "ws_day.txt" using ($9+90):8 with lines    lc rgb "orange"   linewidth 0.5  title "" , \
     "ws_day.txt" using ($9+90):8 with impulses lc rgb "red"      linewidth 2.5 title "Gust" , \
     "ws_day.txt" using ($9+90):7 with lines    lc rgb "#008000"  linewidth 1.5 title "", \
     "ws_day.txt" using ($9+90):7 with impulses lc rgb "#008000"  linewidth 3.0 title "Average" 
#
set output "owdir_week.png"
plot "ws_week.txt" using ($9+90):8 with lines    lc rgb "orange"   linewidth 0.5  title "" , \
     "ws_week.txt" using ($9+90):8 with impulses lc rgb "red"      linewidth 2.5 title "Gust" , \
     "ws_week.txt" using ($9+90):7 with lines    lc rgb "#008000"  linewidth 1.5 title "", \
     "ws_week.txt" using ($9+90):7 with impulses lc rgb "#008000"  linewidth 3.0 title "Average" 
#
set output "owdir_month.png"
plot "ws_month.txt" using ($9+90):8 with lines    lc rgb "orange"   linewidth 0.5  title "" , \
     "ws_month.txt" using ($9+90):8 with impulses lc rgb "red"      linewidth 2.5 title "Gust" , \
     "ws_month.txt" using ($9+90):7 with lines    lc rgb "#008000"  linewidth 1.5 title "", \
     "ws_month.txt" using ($9+90):7 with impulses lc rgb "#008000"  linewidth 3.0 title "Average" 
#
# end of gnuplot input